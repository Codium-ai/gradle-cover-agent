import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.Confidence

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "6.0.21"
}

group = 'ai.codium'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

spotbugs {
    effort = Effort.LESS
    reportLevel = Confidence.valueOf('HIGH')
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'dev.langchain4j:langchain4j-open-ai:0.33.0'
    implementation 'dev.langchain4j:langchain4j:0.33.0'
    implementation 'dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2:0.33.0'
    implementation 'org.tinylog:tinylog-impl:2.6.2'
    implementation 'org.tinylog:slf4j-tinylog:2.6.2'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

gradlePlugin {
    plugins {
        coverAgent {
            id = 'ai.codium.plugin.cover-agent'
            implementationClass = 'ai.codium.cover.plugin.CoverAgentPlugin'
        }
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        html.required.set(true)
        html.outputLocation.set(file("$buildDir/reports/spotbugs/spotbugs.html"))
    }
}

test {
    useJUnitPlatform()
    jacoco {
        excludes = ['sun/util/resources/cldr/provider/CLDRLocaleDataMetaInfo']
    }
}

test {
    ignoreFailures = false
}

checkstyle {
    toolVersion '8.36.1'
}

checkstyleMain {
    ignoreFailures = false
}

checkstyleTest {
    ignoreFailures = true
}



pmdMain {
    ignoreFailures = false
    ruleSets = []
    ruleSetFiles = files("${project.projectDir}/config/pmd/pmd-ruleset.xml")
    excludes = ['BeanMembersShouldSerialize', 'LoosePackageCoupling']
}

pmdTest {
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("${project.projectDir}/config/pmd/pmd-ruleset.xml")
    excludes = ['BeanMembersShouldSerialize', 'LoosePackageCoupling']
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            excludes = [
                    'ai.codium.cover.plugin.CoverAgentPlugin',
                    'ai.codium.cover.plugin.CoverAgentTask',
                    'sun.util.resources.cldr.provider.CLDRLocaleDataMetaInfo'
            ]
            element = 'CLASS'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = new Double(classCoverage)
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = new Double(branchCoverage)
            }
        }
    }
}
spotbugsMain {
    ignoreFailures = false
    dependsOn compileJava
}

spotbugsTest {
    ignoreFailures = true
}


check.finalizedBy jacocoTestReport, jacocoTestCoverageVerification


