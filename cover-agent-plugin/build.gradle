import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.Confidence

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "6.0.21"
}

group = 'ai.codium'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

spotbugs {
    toolVersion = '4.7.2'
    effort = Effort.LESS
    reportLevel = Confidence.valueOf('HIGH')

}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'dev.langchain4j:langchain4j-open-ai:0.33.0'
    implementation 'dev.langchain4j:langchain4j:0.33.0'
    implementation 'dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2:0.33.0'
    implementation 'org.tinylog:tinylog-impl:2.6.2'
    implementation 'org.tinylog:slf4j-tinylog:2.6.2'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    //implementation 'org.jacoco:org.jacoco.cli:0.8.12'
}

gradlePlugin {
    plugins {
        coverAgent {
            id = 'ai.codium.plugin.cover-agent'
            implementationClass = 'ai.codium.cover.plugin.CoverAgentPlugin'
        }
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        html.required.set(true)
        html.outputLocation.set(file("$buildDir/reports/spotbugs/spotbugs.html"))
    }
}

test {
    useJUnitPlatform()
    jacoco {
        excludes = ['sun/util/resources/cldr/provider/CLDRLocaleDataMetaInfo']
    }
}
apply from: "${projectDir}/gradle/static-analysis.gradle"